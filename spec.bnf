program = { statement }

statement = declare-statement | assign-statement | print-statement | if-statement

declare-statement = "let" identifier ":=" comparison

assign-statement = identifier "=" comparison

print-statement = "print" comparison

if-statement = "if" comparison "{" { statement } "}" { else-if-statement } [ else-statement ]

else-if-statement = "else" "if" comparison "{" { statement } "}"

else-statement = "else" "{" { statement } "}"

comparison = expression [ ("==" | "!=" | ">" | ">=" | "<" | "<=") expression ]

expression = term { ("+" | "-") term }

term = unary { ("*" | "/") unary }

unary = "-" unary | factor

factor = "(" expression ")" | number | identifier
